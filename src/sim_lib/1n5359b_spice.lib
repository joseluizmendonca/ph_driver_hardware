* SPICE model Zener diodes ***1N5359B***
* Use the symbol file ***1n5359b.asy***
*
* (c) Diotec Semiconductor AG
* www.diotec.com
* 2017-12-14
*
*********************************************************
* This model is for simulation purposes only. It does   *
* not replace reviewing of the data sheet nor real life *
* testing of the part inside the application.           *
*********************************************************
*
.subckt 1N5359B  A  K  params: Vznom=24 Iztest=0.05 Vr=4.4 Ir=0.5 Rzj=3.5 Izmax=198 Cjo=1280

* Above values are an example for the ***1N5359B***. Using the
* above symbol file allows for direct insertion of other values
* according to these data sheet parameters:
*
* Vznom		Nominal zener voltage at 25°C = (Vzmin+Vzmax)/2
* Iztest	Test current for Vz
* Vr		Reverse voltage (minimum value)
* Ir		Reverse current
* Rzj		Dynamic resistance
* Izmax		Max zener current
* Cjo		Junction capacitance at 0V

Dbr  INT  K  Zbr
Df   A    K  Zf
Dbl  INT  A  DIObl

* Zener breakdown characteristic
.model  Zbr  D(Is={Ir/10} N=2.0 Rs={Rzj} Bv={Vznom*(1+(temp-25)*0.001)} Ibv={Iztest} Eg=1.11 Xti=3 M=.33 diss={Vznom*Izmax})
*
* Forward characteristic
.model  Zf  D(Is=100E-9 N=2.0 Rs=37m Cjo={Cjo} Eg=1.11 Xti=3 M=.33 Tt=800n)
*
* Internal blocking diode
.model  DIObl  D(Ron=.1m)

.ends
